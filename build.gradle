buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

plugins {
	id "com.matthewprenger.cursegradle" version "1.0.10"
	id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.55"
}

apply plugin: 'maven'
apply plugin: 'net.minecraftforge.gradle.forge'

ext.configFile = file "build.properties"

configFile.withReader {
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse prop
}

version = config.mod_version
group = config.mod_group
archivesBaseName = config.mod_name

if (System.getenv().BUILD_NUMBER)
	version = "${config.mod_version}.b${System.getenv().BUILD_NUMBER}"

minecraft {
	version = "${config.mc_version}-${config.forge_version}"
	runDir = "run"
	
	mappings = "${config.mcp_version}"
	makeObfSourceJar = false
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
	maven {
		name = "aroma"
		url = "http://files.aroma1997.org/maven/"
	}
	maven {
		name = "ic2"
		url = "http://maven.ic2.player.to/"
	}
	maven {
		name = "storagedrawers"
		url = "https://dl.bintray.com/jaquadro/dev/"
	}
}

dependencies {
	//core
	compile "aroma1997.core:Aroma1997Core-${config.mc_version}:+:deobf"
	provided "net.industrial-craft:industrialcraft-2:${config.ic2_version}:api"
	
	deobfCompile "com.jaquadro.minecraft.storagedrawers:StorageDrawers:${config.storagedrawers_version}:api"
	runtime "com.jaquadro.minecraft.storagedrawers:StorageDrawers:${config.storagedrawers_version}"
	runtime "com.jaquadro.minecraft.chameleon:Chameleon:${config.chameleon_version}"
}

reobfJar {
	//Instead of setting the accepted mc version to the one we're compiling against, we're setting it to the first one (lowest one) we accept.
	//Forge does the fuzzy detection then.
	getPreTransformers().add(0, new net.minecraftforge.gradle.user.patcherUser.forge.McVersionTransformer(getAvailableMinecraftVersions()[0]))
}

processResources
{
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		include '**/reference.properties'

		expand 'version': version, 'mcversion': config.mc_version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
		exclude '**/reference.properties'
	}
}

task apiJar(type: Jar) {
	from sourceSets.main.output, sourceSets.main.allSource
	include "aroma1997/betterchests/api/**"

	appendix = config.mc_version
	classifier = 'api'
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	appendix = config.mc_version
	classifier = 'deobf'
}

jar {
	appendix = config.mc_version
}

artifacts {
	archives deobfJar
	archives apiJar
}

uploadArchives {
	repositories {
		if (project.hasProperty('maven_dir')) {
			mavenDeployer {
				repository(url: "file://${maven_dir}")
				
			}
		} else {
			println 'Archives upload disabled, gradle.properties is missing.'
		}
	}
}

task signJar(dependsOn: ['deobfJar', 'jar', 'apiJar']){
	doLast {
		// Skip the task if our secret data isn't available
		if (project.hasProperty('signingKeystore')) {
			ant.signjar(
				destDir: "$buildDir/libs",
				jar: "$buildDir/libs/*.jar",
				alias: signingAlias,
				storetype: "jks",
				keystore: signingKeystore,
				storepass: signingStorepass
			)
		} else {
			println("Skipping jar signing. Signing keystore isn't available.")
		}
	}
}
build.dependsOn signJar

def getAvailableMinecraftVersions() {
	if (!config.availableMcVersions) {
		return [ config.mc_version ]
	} else {
		return config.availableMcVersions.split(',')
	}
}

def TEMPORARY_CHANGELOG_FILE = file('changelog/currentchangelog.htm')
def FINISHED_CHANGELOG_FILE = file('changelog/fullchangelog.htm')
task changelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
	doFirst {
		//Get current commit.
		def stdOut = new ByteArrayOutputStream();
		exec {
			commandLine "git", "rev-parse", "HEAD"
			standardOutput = stdOut
		}
		toCommit = stdOut.toString().trim()

		//Get previous commit
		def prevCommitFile = file('changelog/lastcommit.txt')
		if (!prevCommitFile.exists()) {
			fromCommit = toCommit
		} else {
			fromCommit = prevCommitFile.text.trim()
		}
		println "Generating changelog from commit " + fromCommit + " to commit " + toCommit
	}
	file = TEMPORARY_CHANGELOG_FILE
	templateContent = file('changelog/changelog.mustache').getText('UTF-8');
	untaggedName = "Release ${project.version}"

	doLast {
		//Save current commit as previous commit
		def prevCommitFile = file('changelog/lastcommit.txt')
		prevCommitFile.text = toCommit

		//Concate changelogs if something changed
		if (!fromCommit.equals(toCommit)) {
			//Concat changelogs
			if (FINISHED_CHANGELOG_FILE.exists()) {
				FINISHED_CHANGELOG_FILE.text = TEMPORARY_CHANGELOG_FILE.text + FINISHED_CHANGELOG_FILE.text
			} else {
				totalChangelogFile.text = TEMPORARY_CHANGELOG_FILE.text
			}
		} else {
			//Create empty file, because we might need a file for other tasks
			if (!FINISHED_CHANGELOG_FILE.exists()) {
				FINISHED_CHANGELOG_FILE.text = ""
			}
		}
	}
}
build.dependsOn changelog

curseforge {
	apiKey = project.hasProperty('curseforge_apikey') ? project.curseforge_apikey : '0'
	project {
		id = config.curse_project_id
		changelog = "N/A"
		changelogType = 'html'
		releaseType = version.contains('b') ? 'beta' : 'release'
		addArtifact deobfJar
		addArtifact apiJar
		relations {
			requiredLibrary 'aroma1997core'
		}
		getAvailableMinecraftVersions().each { ver -> addGameVersion ver }
	}
}